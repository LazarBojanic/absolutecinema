<program> ::= (<setupDecl> | <sceneDecl> | <varDecl>)* EOF

<setupDecl> ::= "setup" IDENT "{" (<fieldDecl> | <ctorDecl> | <methodDecl> | ";")* "}"
<fieldDecl> ::= "var" IDENT ":" <lType> ("=" <expression>)? ";"
<ctorDecl> ::= IDENT "(" <params>? ")" <block>
<methodDecl> ::= "scene" IDENT "(" <params>? ")" ":" ("scrap" | <lType>) <block>
<sceneDecl> ::= "scene" IDENT "(" <params>? ")" ":" ("scrap" | <lType>) <block>
<varDecl> ::= "var" IDENT ":" <lType> ("=" <expression>)? ";"
<params> ::= <param> ("," <param>)*
<param> ::= ("var")? IDENT ":" <lType>

<lType> ::= ("int" | "double" | "char" | "string" | "bool" | IDENT)("[" "]")*
<rType> ::= ("int" | "double" | "char" | "string" | "bool" | IDENT)("["INT"]")*


<block> ::= "{" (<varDecl> | <statement> | ";")* "}"
<statement> ::= <exprStmt> | <ifStmt> | <whileStmt> | <forStmt> | <returnStmt> | <assignStmt> | <block> | <emptyStmt>
<exprStmt> ::= <expression> ";"
<ifStmt> ::= "if" "(" <expression> ")" <block> ("elif" "(" <expression> ")" <block>)* ("else" <block>)?
<whileStmt> ::= "keepRollingIf" "(" <expression> ")" <block>
<forStmt> ::= "keepRollingDuring" "(" (<varDecl> | <exprStmt> | ";") <expression>? ";" <expression>? ")" <block>
<returnStmt> ::= "cut" <expression>? ";"
<assignStmt> ::= <accessExpression> "=" <expression> ";"
<emptyStmt> ::= ";"


<expression> ::= <assignment>
<assignment> ::= <accessExpression> "=" <assignment> | <logical_or>
<logical_or> ::= <logical_and> ("||" <logical_and>)*
<logical_and> ::= <equality> ("&&" <equality>)*
<equality> ::= <comparison> (("==" | "!=") <comparison>)*
<comparison> ::= <term> (("<" | "<=" | ">" | ">=") <term>)*
<term> ::= <factor> (("+" | "-") <factor>)*
<factor> ::= <unary> (("*" | "/" | "%") <unary>)*
<unary> ::= ("!" | "+" | "-" | "++" | "--") <unary> | <postfix>
<postfix> ::= <primary> ( "(" <arguments>? ")" | "[" <expression> "]" | "." IDENT | "++" | "--" )*
<primary> ::= <literal> | IDENT | "@" | "(" <expression> ")" | <arrayLiteral> | <objectInstantiation>


<accessExpression> ::= (IDENT | "@" | "(" <expression> ")") ( "." IDENT | "[" <expression> "]" | "(" <arguments>? ")" )*


<literal> ::= INT | DOUBLE | STRING | CHAR | "true" | "false" | "null"
<arrayLiteral> ::= "action" <rType> "{" (<expression> ("," <expression>)*)? "}"
<objectInstantiation> ::= "action" <rType> "(" <arguments>? ")"
<arguments> ::= <expression> ("," <expression>)*