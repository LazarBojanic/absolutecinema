<program> ::= <topLevelList> EOF
<topLevelList> ::= <topLevel> <topLevelList> | ε
<topLevel> ::= <setupDecl> | <sceneDecl> | <varDecl>

<setupDecl> ::= "setup" IDENT "{" <setupBody> "}"
<setupBody> ::= <setupMember> <setupBody> | ε
<setupMember> ::= <fieldDecl> | <ctorDecl> | <methodDecl> | ";"

<fieldDecl> ::= "var" IDENT ":" <lType> <optInitializer> ";"
<optInitializer> ::= "=" <expression> | ε

<ctorDecl> ::= IDENT "(" <optParams> ")" <block>
<methodDecl> ::= "scene" IDENT "(" <optParams> ")" ":" <returnType> <block>

<sceneDecl> ::= "scene" IDENT "(" <optParams> ")" ":" <returnType> <block>
<returnType> ::= "scrap" | <lType>

<varDecl> ::= "var" IDENT ":" <lType> <optInitializer> ";"

<optParams> ::= <params> | ε
<params> ::= <param> <paramTail>
<paramTail> ::= "," <param> <paramTail> | ε
<param> ::= <optVar> IDENT ":" <lType>
<optVar> ::= "var" | ε

<lType> ::= <baseType> <arrayDims>
<baseType> ::= "int" | "double" | "char" | "string" | "bool" | IDENT
<arrayDims> ::= "[" "]" <arrayDims> | ε

<rType> ::= <baseType> <rArrayDims>
<rArrayDims> ::= "[" INT "]" <rArrayDims> | ε

<block> ::= "{" <blockItems> "}"
<blockItems> ::= <blockItem> <blockItems> | ε
<blockItem> ::= <varDecl> | <statement> | ";"

<statement> ::= <exprStmt>
              | <ifStmt>
              | <whileStmt>
              | <forStmt>
              | <returnStmt>
              | <assignStmt>
              | <block>
              | ";"

<exprStmt> ::= <expression> ";"

<ifStmt> ::= "if" "(" <expression> ")" <block> <elifList> <optElse>
<elifList> ::= "elif" "(" <expression> ")" <block> <elifList> | ε
<optElse> ::= "else" <block> | ε

<whileStmt> ::= "keepRollingIf" "(" <expression> ")" <block>

<forStmt> ::= "keepRollingDuring"
              "(" <forInit> <optExpression> ";" <optExpression> ")" <block>
<forInit> ::= <varDecl> | <exprStmt> | ";"
<optExpression> ::= <expression> | ε

<returnStmt> ::= "cut" <optExpression> ";"

<assignStmt> ::= <accessExpression> "=" <expression> ";"

<expression> ::= <assignment>

<assignment> ::= <accessExpression> "=" <assignment>
               | <logical_or>

<logical_or> ::= <logical_and> <logical_or_tail>
<logical_or_tail> ::= "||" <logical_and> <logical_or_tail> | ε

<logical_and> ::= <equality> <logical_and_tail>
<logical_and_tail> ::= "&&" <equality> <logical_and_tail> | ε

<equality> ::= <comparison> <equality_tail>
<equality_tail> ::= ("==" | "!=") <comparison> <equality_tail> | ε

<comparison> ::= <term> <comparison_tail>
<comparison_tail> ::= ("<" | "<=" | ">" | ">=") <term> <comparison_tail> | ε

<term> ::= <factor> <term_tail>
<term_tail> ::= ("+" | "-") <factor> <term_tail> | ε

<factor> ::= <unary> <factor_tail>
<factor_tail> ::= ("*" | "/" | "%") <unary> <factor_tail> | ε

<unary> ::= <castExpr>
          | ("!" | "+" | "-" | "++" | "--") <unary>

<castExpr> ::= "int" "(" <expression> ")"
             | "double" "(" <expression> ")"
             | <postfix>

<postfix> ::= <primary> <postfix_tail>
<postfix_tail> ::= "(" <optArguments> ")"
                 | "[" <expression> "]"
                 | "." IDENT
                 | "++"
                 | "--"
                 | ε

<optArguments> ::= <arguments> | ε
<arguments> ::= <expression> <arguments_tail>
<arguments_tail> ::= "," <expression> <arguments_tail> | ε

<primary> ::= <literal>
            | IDENT
            | "@"
            | "(" <expression> ")"
            | <arrayLiteral>
            | <objectInstantiation>

<accessExpression> ::= <primary> <accessTail>
<accessTail> ::= "." IDENT <accessTail>
               | "[" <expression> "]" <accessTail>
               | "(" <optArguments> ")" <accessTail>
               | ε

<literal> ::= INT | DOUBLE | STRING | CHAR | "true" | "false" | "null"

<arrayLiteral> ::= "action" <rType> "{" <optArrayElements> "}"
<optArrayElements> ::= <arrayElements> | ε
<arrayElements> ::= <expression> <arrayElementsTail>
<arrayElementsTail> ::= "," <expression> <arrayElementsTail> | ε

<objectInstantiation> ::= "action" <rType> "(" <optArguments> ")"
