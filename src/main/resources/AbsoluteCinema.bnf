<program> ::= (<setupDecl> | <sceneDecl> | <varDecl>)* EOF

<declaration> ::= <setupDecl> | <sceneDecl> | <varDecl>
<setupDecl> ::= "setup" IDENT "{" (<fieldDecl> | <ctorDecl> | <methodDecl> | ";")* "}"
<fieldDecl> ::= "var" IDENT ":" <type> ("=" <expression>)? ";"
<ctorDecl> ::= IDENT "(" <params>? ")" <block>
<methodDecl> ::= "scene" IDENT "(" <params>? ")" ":" ("scrap" | <type>) <block>
<sceneDecl> ::= "scene" IDENT "(" <params>? ")" ":" ("scrap" | <type>) <block>
<varDecl> ::= "var" IDENT ":" <type> ("=" <expression>)? ";"
<params> ::= <param> ("," <param>)*
<param> ::= ("var")? IDENT ":" <type>

// Types with optional dimensions; empty [] means undefined-size
<type> ::= ("int" | "double" | "char" | "string" | "bool" | IDENT) ("[" INT "]" | "[" "]")*

// Statements
<block> ::= "{" (<varDecl> | <statement> | ";")* "}"
<statement> ::= <exprStmt> | <ifStmt> | <whileStmt> | <forStmt> | <returnStmt> | <assignStmt> | <block> | <emptyStmt>
<exprStmt> ::= <expression> ";"
<ifStmt> ::= "if" "(" <expression> ")" <block> ("elif" "(" <expression> ")" <block>)* ("else" <block>)?
<whileStmt> ::= "keepRollingIf" "(" <expression> ")" <block>
<forStmt> ::= "keepRollingDuring" "(" (<varDecl> | <exprStmt> | ";") <expression>? ";" <expression>? ")" <block>
<returnStmt> ::= "cut" <expression>? ";"
<assignStmt> ::= <accessExpression> "=" <expression> ";"
<emptyStmt> ::= ";"

// Expressions
<expression> ::= <assignment>
<assignment> ::= <accessExpression> "=" <assignment> | <logical_or>
<logical_or> ::= <logical_and> ("||" <logical_and>)*
<logical_and> ::= <equality> ("&&" <equality>)*
<equality> ::= <comparison> (("==" | "!=") <comparison>)*
<comparison> ::= <term> (("<" | "<=" | ">" | ">=") <term>)*
<term> ::= <factor> (("+" | "-") <factor>)*
<factor> ::= <unary> (("*" | "/" | "%") <unary>)*
<unary> ::= ("!" | "+" | "-" | "++" | "--") <unary> | <postfix>
<postfix> ::= <primary> ( "(" <arguments>? ")" | "[" <expression> "]" | "." IDENT | "++" | "--" )*
<primary> ::= <literal> | IDENT | "@" | "(" <expression> ")" | <arrayLiteral> | <objectInstantiation>

// Access (for assignment LHS)
<accessExpression> ::= (IDENT | "@" | "(" <expression> ")") ( "." IDENT | "[" <expression> "]" | "(" <arguments>? ")" )*

// Literals and special expressions
<literal> ::= INT | DOUBLE | STRING | CHAR | "true" | "false" | "null"
<arrayLiteral> ::= "action" <type> "{" (<expression> ("," <expression>)*)? "}"
<objectInstantiation> ::= "action" <type> "(" <arguments>? ")"
<arguments> ::= <expression> ("," <expression>)*